apply plugin: 'java'
apply plugin: 'maven'

group = 'com.jme3x'
version = '1.6.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.jmeVersion = "3.2_branch-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
    compile 'com.github.JavaSaBr:RLib:6.3.3'
    compile "com.github.JavaSaBr.jmonkeyengine:jme3-core:$jmeVersion"
    compile "com.github.JavaSaBr.jmonkeyengine:jme3-lwjgl3:$jmeVersion"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

defaultTasks 'clean', 'install'

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.version = version
        pom.artifactId = 'jme-jfx'
        pom.project {
            licenses {
                license {
                    name 'License is the New BSD License (same as JME3) '
                    url 'http://opensource.org/licenses/BSD-3-Clause'
                    distribution 'repo'
                }
            }
        }
    }
}