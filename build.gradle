buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'com.jme3x'
version = '2.0.0'

compileJava {
    options.encoding = "UTF-8"
    options.incremental = true
    options.compilerArgs.addAll([
        "--add-exports",
        "javafx.graphics/com.sun.javafx.embed=ALL-UNNAMED"
    ])
    options.compilerArgs.addAll([
        "--add-exports",
        "javafx.graphics/com.sun.javafx.stage=ALL-UNNAMED"
    ])
    options.compilerArgs.addAll([
        "--add-exports",
        "javafx.graphics/com.sun.javafx.cursor=ALL-UNNAMED"
    ])
    options.compilerArgs.addAll([
        "--add-exports",
        "javafx.graphics/com.sun.glass.ui=ALL-UNNAMED"
    ])
    options.compilerArgs.addAll([
        "--add-exports",
        "javafx.graphics/com.sun.javafx.application=ALL-UNNAMED"
    ])
}

sourceCompatibility = 1.10
targetCompatibility = 1.10

ext.rlibVersion = "7.0.0"
ext.bintrayVersion = version

def jme3 = [v: '3.2.0-stable', g: 'org.jmonkeyengine']

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url  "https://dl.bintray.com/javasabr/maven"
    }
}

publishing {
    publications {
        LibArtifact(MavenPublication) {
            from components.java
            groupId group
            artifactId 'jme-jfx'
            version bintrayVersion
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getProperty('bintrayUser')
    key = System.getProperty('bintrayKey')
    publications = ['LibArtifact']
    pkg {
        repo = 'maven'
        name = 'com.jme3x.jme-jfx'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/JavaSaBr/JME3-JFX'
        vcsUrl = 'https://github.com/JavaSaBr/JME3-JFX.git'
        issueTrackerUrl = 'https://github.com/JavaSaBr/JME3-JFX/issues'
        githubRepo = 'JavaSaBr/JME3-JFX'
        githubReleaseNotesFile = 'README.md'
        version {
            name = bintrayVersion
            desc = bintrayVersion
            released  = new Date()
        }
    }
}

dependencies {
    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
    compile group: 'com.spaceshift', name: 'rlib.common', version: rlibVersion
    compile "${jme3.g}:jme3-core:${jme3.v}"
    compile "${jme3.g}:jme3-lwjgl3:${jme3.v}"
}

javadoc {
    failOnError = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}